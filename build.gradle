plugins {
    id 'java'
    id 'groovy'
    id 'maven-publish'
    id 'signing'
    id "io.freefair.lombok" version "6.2.0"
}

group 'com.stiltfox'
version '0.12.9'

repositories {
    mavenCentral()
}

dependencies {
    //regular dependencies
    implementation 'com.fasterxml.jackson.core:jackson-databind:2.13.4'
    implementation 'com.h2database:h2:1.4.200'

    //test dependencies
    testImplementation 'org.spockframework:spock-core:2.0-groovy-3.0'
    testImplementation 'org.codehaus.groovy:groovy-all:3.0.8'
    testImplementation 'org.wiremock:wiremock:3.3.1'
}

test {
    useJUnitPlatform()
}

jar {
    manifest {
        attributes 'Implementation-Version' : this.version
    }
}

publishing {
    publications {
        maven(MavenPublication) {
            from components.java
        }
    }
    repositories {
        maven {
            name = "StiltFoxRepository"
            url = property('StiltFoxRepositoryUrl')
            credentials(PasswordCredentials)
            authentication {
                basic(BasicAuthentication)
            }
        }
    }
}
